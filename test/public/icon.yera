-- @author Francesco Bracchi
-- @email frbracch@gmail.com
-- @desc an icon is a clickable object that makes some special effects.
        
upto s = st until st >= 1 and const 1
    where 
      st = min 1 s;
    end;

ram t = upto (local / t);

smooth t a b = a + (b - a) * t;

img src d t = 
    let dt = round (24 * (1 - d));
        x =  48 - 2 * dt;
        st = {
          opacity: t,
          position: "relative",
          top:   dt + "px",
          left:  dt + "px",
          width:  x + "px",
          height: x + "px"
        };
    in `<div style = {width: "48px", height: "48px", textAlign: "left"}>
        <img class = "icon" src = src style = st/>
        </div>;
      
clicked src d0 t0 = i until (onclick || onterm || onexit)
    where
      r = ram 100;
      d = smooth r d0 1.2;
      t = smooth r t0 1;
      i = img src d t;
      onclick = (lbc over i) && (lift clicked) (futr src) (pres d) (pres t);
      onexit  = (exit i) && (lift normal) (futr src) (pres d) (pres t);
      onterm  = r == 1 && (lift entered) (futr src) (pres d) (pres t);
    end;
       
normal src d0 t0 = i until onenter
    where
      r = ram 400;
      d = smooth r d0 0.8;
      t = smooth r t0 0.5;
      i = img src d t;
      onenter = (enter i) && (lift entered) (futr src) (pres d) (pres t);
    end;

entered src d0 t0 = i until (onexit || onclick)
    where
      r = ram 400;
      d = smooth r d0 1;
      t = smooth r t0 1;
      i = img src d t;
      onexit = (exit i) && (lift normal) (futr src) (pres d) (pres t);
      onclick = (lbc over i) && (lift clicked) (futr src) (pres d) (pres t);
    end;

icon src = normal src 0 0;
